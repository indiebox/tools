#!/usr/bin/perl
#
# Check that packages in a directory hierarchy follow the UBOS
# conventions.
#
# Copyright (C) 2018 and later, Indie Computing Corp. All rights reserved. License: see package.
#

use strict;
use warnings;

use File::Find;
use Getopt::Long;
use UBOS::Logging;
use UBOS::Utils;

my $urlRegex = '^https?://';

my %REQUIRED_REGEXES = (
    'url'        => $urlRegex,
    'maintainer' => $urlRegex,
    'pkgname'    => '^[-_a-z0-9]+$',
    'pkgver'     => '^[0-9a-zA-Z_.:]+$',
    'pkgrel'     => '^[0-9]+$',
    'pkgdesc'    => '.',
    'arch'       => '^(any|x86_64|armv6h|armv7h|aarch64)(\s+(any|x86_64|armv6h|armv7h|aarch64))*$',
    'license'    => '^(GPL|GPL2|GPL3|AGPL|AGPL2|AGPL3|Apache|EPL|PerlArtistic|RUBY|custom:\S+)(\s+(GPL|GPL2|GPL3|AGPL|AGPL2|AGPL3|Apache|EPL|PerlArtistic|RUBY|custom:\S+))*$'
);
my %OPTIONAL_REGEXES = (
    'developer'   => $urlRegex,
    'releasepage' => $urlRegex
);

my @REQUIRED_FIELDS = keys %REQUIRED_REGEXES;
my @OPTIONAL_FIELDS = keys %OPTIONAL_REGEXES;

my %EXTENSIONS_NEED_COPYRIGHT = (
    'c'    => 1,
    'java' => 1,
    'php'  => 1,
    'pl'   => 1,
    'pm'   => 1,
    'sql'  => 1,
    'tmpl' => 1,
    'txt'  => 1,
    'xml'  => 1
);

my $COPYRIGHTLINE = "\QCopyright (C)\E \\d\\d\\d\\d \Qand later, Indie Computing Corp. All rights reserved. License: see package.\E";

my %ARCHIVE_EXTENSIONS = (
    'bz2'  => 1,
    'gz'   => 1,
    'gzip' => 1,
    'tgz'  => 1,
    'xz'   => 1,
    'zip'  => 1
);

my $verbose       = 0;
my $logConfigFile = undef;
my $help          = 0;
my $exitCode      = 0;

my $parseOk = GetOptions(
        'verbose+'    => \$verbose,
        'logConfig=s' => \$logConfigFile,
        'help'        => \$help );

UBOS::Logging::initialize( 'check-pkgbuilds', undef, $verbose, $logConfigFile, 0 );

my @dirs = @ARGV;

if( $help ) {
    helpQuit();
}    
if(    !$parseOk
    || ( $verbose && $logConfigFile )
    || !@dirs )
{
    $exitCode = 1;
    helpQuit();
}

my @pkgbuilds = ();
find(  {
           'wanted' => sub {
                if( m/PKGBUILD/ ) {
                    push @pkgbuilds, $File::Find::name;
                }
            },
            'preprocess' => sub {
                return sort( @_ );
            }
        },
        @dirs );

foreach my $pkgbuild ( @pkgbuilds ) {
    processPkgbuild( $pkgbuild );
}

exit $exitCode;

#####
# A PKGBUILD was found
# $pkgbuild: the PKGBUILD file with path
sub processPkgbuild {
    my $filename = shift;

    my $dir = $filename;
    if( $dir =~ m!^(.*/)! ) { # greedy
        $dir = $1;
    }

    info( 'Processing', $filename );

    # We are already in the correct directory

    checkPkgbuild( $dir );
    checkCopyright( $dir );
}

#####
# Check that the PKGBUILD in the provided directory is valid
# $dir: the directory that contains the PKGBUILD
sub checkPkgbuild {
    my $dir = shift;

    my $bashScript = "cd '$dir'\n";
    $bashScript .= <<'SCRIPT';
. PKGBUILD
echo developer=${developer:-x_x_x}
echo url=${url:-x_x_x}
echo maintainer=${maintainer:-x_x_x}
echo pkgname=${pkgname:-x_x_x}
echo pkgver=${pkgver:-x_x_x}
echo pkgrel=${pkgrel:-x_x_x}
echo pkgdesc=${pkgver:-x_x_x}
echo arch=${arch[@]:-x_x_x}
echo license=${license[@]:-x_x_x}
echo depends=${depends[@]:-x_x_x}
echo backup=${backup[@]:-x_x_x}
echo releasepage=${releasepage:-x_x_x}
SCRIPT

    my $out;
    my $values = {};

    if( UBOS::Utils::myexec( '/bin/bash', $bashScript, \$out )) {
        error( 'Failed to read PKGBUILD in:', $dir );
    } else {
        foreach my $line ( split( /\n/, $out )) {
            if( $line =~ m!^([^=]+)=(.*)$! ) {
                $values->{$1} = $2;
            }
        }
    }

    foreach my $required ( @REQUIRED_FIELDS ) {
        if( $values->{$required} eq 'x_x_x' ) {
            error( 'PKGBUILD in', $dir, 'is missing required field:', $required );
        } elsif( $values->{$required} !~ m!$REQUIRED_REGEXES{$required}! ) {
            error( 'PKGBUILD in', $dir, ', field:', $required, 'does not match regex', $REQUIRED_REGEXES{$required}, 'is:', $values->{$required} );
        }
    }
    foreach my $optional ( @OPTIONAL_FIELDS ) {
        if( $values->{$optional} ne 'x_x_x' && $values->{$optional} !~ m!$OPTIONAL_REGEXES{$optional}!) {
            error( 'PKGBUILD in', $dir, ', field:', $optional, 'does not match regex', $OPTIONAL_REGEXES{$optional}, 'is:', $values->{$optional} );
        }
    }

    if( $values->{'license'} =~ m!^custom:(.*)$! ) {
        my $licenseName = $1;
        if( $licenseName ne $values->{'pkgname'} ) {
            error( 'Custom license name must be name of the package: PKGBUILD in', $dir );
        }

        my $foundArchive = 0;
        foreach my $arch ( split /\s+/, $values->{'arch'} ) {

            my $archiveName = $values->{'pkgname'} . '-' . $values->{'pkgver'} . '-'
                    . $values->{'pkgrel'} . '-' . $arch . '.pkg.tar.xz';

            if( -e "$dir$archiveName" ) {
                $foundArchive = 1;
                UBOS::Utils::myexec( "tar xfJ '$dir$archiveName' 'usr/share/licenses/'" . $values->{'pkgname'} . ' -O', undef, \$out );
                if( length( $out ) < 100 ) {
                    error( 'Custom license file for package appaears to short;', $dir );
                }
            }
        }

        unless( $foundArchive ) {
            warning( 'Archive does not exist in:', $dir );
        }
    }
}

#####
# Check that all relevant files have the right copyright notice
# $dir: the directory that contains the PKGBUILD
sub checkCopyright {
    my $dir = shift;

    my @skipList = qw( src pkg ubos-manifest.json PKGBUILD LICENSE LICENSE.txt );

    if( -e "$dir/.ubos-check-packages-ignore" ) {
        my $ignore = UBOS::Utils::slurpFile( "$dir/.ubos-check-packages-ignore" );
        push @skipList, grep{ $_ } split( /\n/, $ignore );
    }

    trace( 'Skip list in', $dir, ':', @skipList );

    find(
        {
            'no_chdir' => 1,
            'wanted' => sub {
                unless( -f $File::Find::name ) {
                    return;
                }
                my $relativeName = $File::Find::name;
                $relativeName =~ s!^$dir!!;
                my $ext = undef;
                if( $relativeName =~ m!\.([^.]+)$! ) {
                    $ext = $1;
                }

                # This is a heuristic here

                foreach my $skip ( @skipList ) {
                    if( length( $relativeName ) >= length( $skip )) {
                        if( $relativeName eq $skip ) {
                            return;
                        }
                        if( substr( $relativeName, length( $skip ), 1 ) eq '/' ) {
                            return;
                        }
                    }
                }
                if( $ext && exists( $ARCHIVE_EXTENSIONS{$ext} )) {
                    return;
                }

                my $content = UBOS::Utils::slurpFile( "$dir$relativeName" );
                my $foundCopyright = 0;
                while( $content =~ m!^(.*\(C\).*)$!mg ) {
                    my $line = $1;

                    $foundCopyright = 1;

                    if( $line !~ m!$COPYRIGHTLINE! ) {
                        error( 'Fix copyright in:', "$dir$relativeName", ', is', $line );
                    }
                }
                if( !$foundCopyright && $ext && $EXTENSIONS_NEED_COPYRIGHT{$ext} ) {
                    error( 'Needs copyright but does not have it:', "$dir$relativeName" );
                }
            },
            'preprocess' => sub {
                return sort( @_ );
            }
        },
        $dir );
}

#####

sub helpQuit {
    print STDERR <<END;
Synopsis:
    $0 [--verbose | --logConfig <logConfig> ] <dir>...
        <dir> -- one or more directories in which to recursively look for PKGBUILD files,
                 which indicate the top-level directory of a package
    $0 --help
        This help.
END
    exit 0;
}

1;

