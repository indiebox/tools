#!/usr/bin/perl
#
# Provides a 360-degree view of the status of one or more git directories
# with respect to other branches and tags
#

use strict;
use warnings;

use Cwd;
use File::Basename;
use Getopt::Long;

my $statusArg;
my $tagsArg;
my $branchesArg;
my $allArg;
my @wrtArg = ();

my $parseOk = GetOptions(
        'status'             => \$statusArg,
        'tags'               => \$tagsArg,
        'branches'           => \$branchesArg,
        'all'                => \$allArg,
        'compare-with-tag=s' => \@wrtArg );

my @dirs = @ARGV;
if(    !$parseOk
    || !@dirs
    || ( $allArg && ( $statusArg || $tagsArg || $branchesArg ))
    || ( @wrtArg && !( $allArg || $tagsArg )))
{
    print STDERR <<END;
Synopsis:
    $0 [--status] [--tags [ --compare-with-tag <tag> ]] [--branches] <dir>...
    $0 --all <dir>...
    $0 ... <dir>...
END
    exit 1;
}

my %wrt = ();
map { $wrt{$_} = $_ } @wrtArg;

if( $allArg ) {
    $statusArg   = 1;
    $tagsArg     = 1;
    $branchesArg = 1;
} elsif( !$statusArg && !$tagsArg && !$branchesArg ) {
    $statusArg = 1;
}

foreach my $d ( @dirs ) {
    if( isGitDir( $d )) {
        print "=== $d ===\n";

        if( $statusArg ) {
            my @status   = split( "\n", `cd $d; git status -s` );
            if( @status ) {
                print "Status:\n";
                print map { "    $_\n" } @status;
            } else {
                print "Status: clean\n";
            }
        }
        
        if( $branchesArg ) {
            my @branches = split( "\n", `cd $d; git branch` );
            print "Branches:\n";
            print map { "    $_ (" . info( $d, $_, undef )  . ")\n" } @branches;
        }

        if( $tagsArg ) {
            my @tags     = split( "\n", `cd $d; git tag` );
            if( @tags ) {
                print "Tags:\n";
                print map { "    $_ (" . info( $d, $_, \%wrt )  . ")\n" } @tags;
            } else {
                print "No tags\n";
            }
        }
        print "\n";
    } else {
        print "WARNING: $d is not a git directory\n";
    }
}

sub isGitDir {
    my $d = shift;

    if( -d "$d/.git" ) {
        return 1;
    }
    $d = Cwd::abs_path( $d );
    if( $d eq '/' ) {
        return 0;
    }
    return isGitDir( dirname( $d ));
}

sub info {
    my $d      = shift;
    my $tag    = shift;
    my $toShow = shift;

    my $ret;
    if( $tag =~ m!\*! ) {
        $ret = "this";
    } elsif( !defined( $toShow ) || defined( $toShow->{$tag} )) {
        $ret = `cd $d; git diff --shortstat $tag`;
        $ret =~ s!^\s+!!;
        $ret =~ s!\s+$!!;
        unless( $ret ) {
            $ret = "same";
        }
    } else {
        $ret = "?";
    }
    return $ret;
}

1;

