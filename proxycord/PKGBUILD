developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=$developer
pkgname=$(basename $(pwd))
pkgver=0.1
pkgrel=1
pkgdesc="Records a web browsing session through a proxy"
arch=('any')
license=('AGPL3')
makedepends=(
    'maven'
    'jdk8-openjdk'
    'perl')
depends=(
    'java-runtime')
options=('!strip')

prepare() {
    # Set pom.xml versions correctly; depends on XML-comment-based markup in pom.xml files
    find ${startdir} -name pkg -and -type d -prune -or -name pom.xml -exec perl -pi -e "s/(?<=\<\!-- PKGVER -->)(\d+(\.\d+)+)(?=\<\!-- \/PKGVER -->)/${pkgver}/g" {} \;
}

build() {
    cd ${startdir}/net.ubos.proxycord
    mvn package -Ppkgbuild ${MVN_OPTS}
}

package() {
    installJar 'net.ubos.proxycord'

    install -D -m644 ${startdir}/logging.properties ${pkgdir}/etc/${pkgname}/logging.properties

    install -D -m755 ${startdir}/bin/proxycord -t ${pkgdir}/usr/bin/
    perl -pi -e "s!\\\$\{PKGVER\}!${pkgver}!g" ${pkgdir}/usr/bin/proxycord
}

installJar() {
    local name=$1
    install -m644 -D ${startdir}/src/maven-target/${name}-${pkgver}.jar ${pkgdir}/usr/lib/java/net/ubos/${name}/${pkgver}/${name}-${pkgver}.jar
}
